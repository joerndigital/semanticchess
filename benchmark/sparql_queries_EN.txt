PREFIX ex: <http://example.com/>
PREFIX res: <http://example.com/res/>
PREFIX prop: <http://example.com/prop/>

========================================================
1	Show me games by Wilhelm Steinitz.
SELECT DISTINCT ?game
WHERE {
	?game prop:white|prop:black 'Wilhelm Steinitz' .
}

========================================================
2	Show me all won games of Emanuel Lasker against Wilhelm Steinitz.
SELECT DISTINCT ?game
WHERE {
	{?game prop:black 'Wilhelm Steinitz' .
	 ?game prop:white 'Emanuel Lasker' .
	 ?game prop:result '1-0' .
	}
	UNION
	{?game prop:white 'Wilhelm Steinitz' .
	 ?game prop:black 'Emanuel Lasker' .
	 ?game prop:result '0-1' .
	}
}

========================================================
3	Which player with an ELO above 2500 has won the most in 1899?
SELECT ?player (count(?game) AS ?nr)
WHERE {
	{?game prop:white ?player .
	 ?game prop:result '1-0' .
	 ?game prop:date ?date .
	 FILTER regex(?date, '1899')
	}
	UNION
	{?game prop:black ?player .
	 ?game prop:result '0-1' .
	 ?game prop:date ?date .
	 FILTER regex(?date, '1899')
	}
} 
GROUP BY ?player
ORDER BY DESC(?nr)
LIMIT 3 OFFSET 0

========================================================
4	Have Emanuel Lasker played the opening King’s Indian?
ASK {
	?game prop:black|prop:white 'Emanuel Lasker' .
	?game prop:eco ?eco .
	FILTER regex(?eco, '^E[6-9]{2}.*')
}

========================================================
5	Give me 10 blitz games of Wilhelm Steinitz.
SELECT ?game
WHERE {
	?game prop:black|prop:white 'Wilhelm Steinitz' .
	?game prop:event ?event .
	FILTER regex(?event, 'blitz', 'i')
}

========================================================
6	Show me endgames with rook against rook and pawn.
SELECT DISTINCT ?game
WHERE {
	 ?game prop:moves ?moves .
	 ?moves prop:fen ?fen .
	 FILTER (regex(?fen, '^[^BbNnQqP]* w|b  ') && regex(?fen, '^[^p]+p{1}[^p]+ ') && regex(?fen, '^[^r]*r{1}[^r]* ') && regex(?fen, '^[^R]*R{1}[^R]* '))		
}


========================================================
7	In which tournaments did Adolf Anderssen and Wilhelm Steinitz play against each other?
SELECT DISTINCT ?event
WHERE {
	?game prop:black|prop:white 'Adolf Anderssen' .
	?game prop:black|prop:white 'Wilhelm Steinitz' .
	?game prop:event ?event .
}

========================================================
8	All losing games of Wilhelm Steinitz from London.
SELECT DISTINCT ?game
WHERE {
	{?game prop:black 'Wilhelm Steinitz' .
	 ?game prop:result '1-0' .
	 ?game prop:site ?site .
	 FILTER regex(?site, 'London', 'i')
	 }
	 UNION
	{?game prop:white 'Wilhelm Steinitz' .
	 ?game prop:result '0-1' .
	 ?game prop:site ?site .
	 FILTER regex(?site, 'London', 'i')
	 }	 
}

========================================================
9	Give me 5 games with an underpromotion.
SELECT DISTINCT ?game
WHERE {
	?game prop:moves ?moves .
	?moves prop:move ?move .
	FILTER regex(?move, '[a-h]8[BNRbnr]')
}
LIMIT 5 OFFSET 0

========================================================
10	Show me all games of the world championship 1886.
SELECT DISTINCT ?game
WHERE {
	?game prop:event ?event .
	?game prop:date ?date .
	FILTER (regex(?event, 'World Championship', 'i') && regex(?date, '1886', 'i'))
}

========================================================
11	What is the longest game?
SELECT DISTINCT ?game
WHERE {
	?game prop:moves ?moves .
	?moves prop:moveNr ?nr .
}
ORDER BY DESC(?nr) LIMIT 1 OFFSET 0

========================================================
12 Give me games where Adolf Anderssen defeats Howard Staunton with black.
SELECTT DISTINCT ?game
WHERE {
	?game prop:white 'Adolf Anderssen'.
	?game prop:black 'Howard Staunton'.
	?game prop:result '1-0'.
}


========================================================
13	What is the first game of Adolf Anderssen?
SELECT DISTINCT ?game
WHERE {
	?game prop:black|prop:white 'Adolf Anderssen' .
	?game prop:date ?date .
}
ORDER BY ASC(?date) LIMIT 1 OFFSET 0

========================================================
14	Show me 10 games of white in the Queen’s Indian.
SELECT DISTINCT ?game
WHERE {
	?game prop:eco ?eco .
	FILTER regex(?eco, 'E1[2-9].*')
}
LIMIT 10

========================================================
15	Which opening has Wilhelm Steinitz played most frequently?
SELECT DISTINCT ?eco (COUNT(?eco) AS ?nr)
WHERE {
	?game prop:white|prop:black 'Wilhelm Steinitz' .
	?game prop:eco ?eco .
}
GROUP BY ?eco
ORDER BY DESC(?nr)
LIMIT 1 OFFSET 0

========================================================
16	In which game did Caruana have his highest rating?
SELECT DISTINCT ?game
WHERE {
	{?game prop:black 'Fabiano Caruana' .
	 ?game prop:blackelo ?elo .
	}
	UNION
	{?game prop:white 'Fabiano Caruana' .
	 ?game prop:whiteelo ?elo .
	}
}
ORDER BY DESC(?elo) 
LIMIT 1

========================================================
17	Show me all the games of the tournament with the highest average rating.
SELECT DISTINCT ?game
WHERE {
	?game prop:event ?event .
	{SELECT DISTINCT ?event
	 WHERE {
		?game prop:event ?event . 
		?game prop:whiteelo|prop:blackelo ?elo .
	 }
	 GROUP BY ?event
	 ORDER BY DESC(AVG(?elo))
	 LIMIT 1
	 }
}

========================================================
18	Give me the games in which Wilhelm Steinitz lost against a much weaker player.
SELECT DISTINCT ?game
WHERE {
	{?game prop:black 'Wilhelm Steinitz' .
	 ?game prop:result '1-0' .
	 ?game prop:whiteelo ?elo .
	}
	UNION
	{?game prop:white 'Wilhelm Steinitz' .
	 ?game prop:result '0-1' .
	 ?game prop:blackelo ?elo .
	}
}
ORDER BY ASC(?elo)
LIMIT 10 OFFSET 0

========================================================
19	What openings were played at the World Championship 1894?
SELECT DISTINCT ?eco
WHERE {
	?game prop:event ?event .
	?game prop:date ?date .
	?game prop:eco ?eco .
	FILTER regex(?event, 'World Championship' ,'i')
	FILTER regex(?date, '1894', 'i')
}
ORDER BY ASC(?eco)

========================================================
20	How many times has Adolf Anderssen played draw?
SELECT DISTINCT (COUNT(?game) AS ?draw)
WHERE {
	?game prop:black|prop:white 'Adolf Anderssen' .
	?game prop:result '1/2-1/2' .
}

========================================================
21	Which game was played exactly 10 years ago?
==> inaccurate, month and day have to be considered

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT DISTINCT ?game ?year
WHERE {
	?game prop:date ?date .
	BIND( substr( ?date, 1, 4 ) as ?year )
	FILTER (xsd:Integer(?year) + 150 = 2017)
}

========================================================
22	What does Emanuel Lasker play against the Sicilian Defense?
SELECT DISTINCT ?game ?move
WHERE {
	?game prop:black|prop:white 'Emanuel Lasker' .
	?game prop:eco ?eco .
	?game prop:moves ?moves .
	?moves prop:move ?move .
	?moves prop:moveNr ?nr .
	FILTER regex(?eco, 'B[2-9][0-9].*')
}
ORDER BY ?game ASC(?nr)

========================================================
23	Who was the 8th world champion?
SELECT DISTINCT ?player (COUNT(?result) + ((COUNT(?draw))/2) AS ?points)
WHERE {
	{?game prop:black ?player .
	 ?game prop:event '7. World Championship' .
	 ?game prop:result '0-1' .
	 ?game prop:result ?result .
	}
	UNION
	{?game prop:white ?player .
	 ?game prop:event '7. World Championship' .
	 ?game prop:result '1-0' .
	 ?game prop:result ?result .
	}
	UNION
	{?game prop:black|prop:white ?player .
	 ?game prop:event '7. World Championship' .
	 ?game prop:result '1/2-1/2' .
	 ?game prop:result ?result .
	}
}
GROUP BY ?player
ORDER BY DESC(?points)
LIMIT 5 OFFSET 0

========================================================
24	What was the last game of Wilhelm Steinitz?
SELECT DISTINCT ?game
WHERE {
	?game prop:black|prop:white 'Wilhelm Steinitz' .
	?game prop:date ?date .
}
ORDER BY DESC(?date) LIMIT 1 OFFSET 0


========================================================
25	How often is King’s Indian played?
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT DISTINCT (?played/(xsd:double(COUNT(?ecoAll))*100)  AS ?percentage)
WHERE {
	?gamesAll prop:eco ?ecoAll .
	{
		SELECT DISTINCT (COUNT(?eco) AS ?played)
		WHERE {
			?game prop:eco ?eco .
        FILTER regex(?eco, '^E[6-9]{2}.*')
		}
	}
} GROUP BY ?played

========================================================
26	When did Wilhelm Steinitz played his last World Championship?
SELECT ?date
WHERE {
	?game prop:date ?date .
	?game prop:white|prop:black 'Wilhelm Steinitz' .
	?game prop:event ?event.
	FILTER regex(?event, 'World Championship', 'i')
}
ORDER BY DESC(?date)
LIMIT 1 OFFSET 0
========================================================
27	Which players with black defeated Howard Staunton?
SELECT ?player 
WHERE {
	?game prop:black ?player .
	?game prop:white 'Howard Staunton' .
	?game prop:result '0-1'.
}

========================================================
28	All games with a move number exceeding 100.
SELECT DISTINCT ?game
WHERE {
	?game prop:moves ?moves .
	?moves prop:moveNr ?nr .
	FILTER ((?nr/2) > 99)
} GROUP BY ?game

========================================================
29	Show me any games with three pawns on the 7th rank?
SELECT DISTINCT ?game
WHERE {
	{?game prop:moves ?moves	.
	 ?moves prop:fen ?fen .
	 FILTER regex(?fen, '^.*/.*P{1}.*P{1}.*P{1}.*/.*/.*/.*/.*/.*/.* ')
	}
	UNION
	{?game prop:moves ?moves	.
	 ?moves prop:fen ?fen .
	 FILTER regex(?fen, '^.*/.*/.*/.*/.*/.*/.*p{1}.*p{1}.*p{1}.*/.* ')
	}
}

========================================================
30	Show me positions with knight pair against bishop pair.
SELECT DISTINCT ?game
WHERE {
	{?game prop:moves ?moves .
	 ?moves prop:fen ?fen .
	 FILTER (regex(?fen, '^[^bNRrQq]* w|b  ') && regex(?fen, '^[^B]*B{2}[^B]* ') && regex(?fen, '^[^n]*n{2}[^n]* '))
	}
	UNION
	{?game prop:moves ?moves .
	 ?moves prop:fen ?fen .
	 FILTER (regex(?fen, '^[^BnQqp]* w|b  ') && regex(?fen, '^[^b]+b{2}[^b]+ ') && regex(?fen, '^[^N]*N{2}[^N]* '))
	}	
}

========================================================
31	Show me wins in C33.
SELECT ?game
WHERE {
	{?game prop:eco 'C33'.
	?game prop:result '1-0'.
	}
	UNION
	{?game prop:eco 'C33'.
	?game prop:result '0-1'.
	}
}

========================================================
32	Has Carlsen ever played a fianchetto?
ASK {?game prop:black|prop:white 'Magnus Carlsen'.
	{?game prop:moves ?moves .
	?moves prop:fen ?fen .
	FILTER regex(?fen, '^.*/.*/.*/.*/.*/.*P{1}.{1}/.*PBP/.* ')
	}
	UNION
	{?game prop:moves ?moves .
	?moves prop:fen ?fen .
	FILTER regex(?fen, '^.*/.*PBP/.*P{1}.{1}/.*/.*/.*/.*/.* ')
	}
	UNION
	{?game prop:moves ?moves .
	?moves prop:fen ?fen .
	FILTER regex(?fen, '^.*/.*/.*/.*/.*/.{1}P{1}.*/PBP.*/.* ')
	}
	UNION
	{?game prop:moves ?moves .
	?moves prop:fen ?fen .
	FILTER regex(?fen, '^.*/PBP.*/.{1}P{1}.*/.*/.*/.*/.*/.* ')
	}
}

========================================================
33	Are there games with f2-f4 d7-d5 g2-g3?
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT DISTINCT ?game
WHERE {
	?game prop:moves ?move1 .
	?game prop:moves ?move2 .
	?game prop:moves ?move3 .
	
	?move1 prop:moveNr '1'^^xsd:nonNegativeInteger .
	?move2 prop:moveNr '2'^^xsd:nonNegativeInteger .
	?move3 prop:moveNr '3'^^xsd:nonNegativeInteger .
	
	?move1 prop:move 'f2-f4'.
	?move2 prop:move 'd7-d5'.
	?move3 prop:move 'g2-g3'.
}

========================================================
34	Wins of white in B33 and an average ELO of over 2400.
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT DISTINCT ?game
WHERE {
	?game prop:blackelo|prop:whiteelo ?elo .
	?game prop:eco 'B33' .
	?game prop:result '1-0'.
}
GROUP BY ?game
HAVING (AVG(?elo)>'2400'^^xsd:int)

========================================================
35	 Which player loses the most with white.
SELECT ?player (count(?game) as ?nr)
WHERE {
	?game prop:white ?player .
	?game prop:result '0-1' .
}
GROUP BY ?player
ORDER BY DESC(?nr)

========================================================
36	What was the first game with a Ruy Lopez?
SELECT DISTINCT ?game
WHERE {
	?game prop:eco ?eco .
	?game prop:date ?date .
	FILTER regex(?eco, '^C[6-9][0-9].*')
}
ORDER BY ASC(?date)
LIMIT 1 OFFSET 0

========================================================
37	Show me the 3rd game between Wilhelm Steinitz and Emanuel Lasker.
SELECT ?game
WHERE {
	?game prop:white|prop:black 'Wilhelm Steinitz'.
	?game prop:white|prop:black 'Emanuel Lasker'.
	?game prop:date ?date.
}
ORDER BY ASC(?date)
LIMIT 1 OFFSET 2 



========================================================
38	All games of GMs against computers.
SELECT DISTINCT ?game
WHERE {
	?game prop:black|prop:white ?comp .
	?game prop:black|prop:white ?player .
	?game prop:blacktitle|prop:whitetitle 'GM' .
	FILTER regex(?comp, 'Comp', 'i') 
}

========================================================
39	What is the lifetime record between Wilhelm Steinitz and Emanuel Lasker.
SELECT DISTINCT (count(?win) as ?wins) (count(?lose) as ?losts) (count(?draw) as ?draws)
WHERE {
	{?game prop:black 'Wilhelm Steinitz' .
	 ?game prop:white 'Emanuel Lasker' .
	 ?game prop:result '0-1' .
	 ?game prop:result ?win .
	}
	UNION
	{?game prop:white 'Wilhelm Steinitz' .
	 ?game prop:black 'Emanuel Lasker' .
	 ?game prop:result '1-0' .
	 ?game prop:result ?win .
	}
	UNION
	{?game prop:black 'Wilhelm Steinitz' .
	 ?game prop:white 'Emanuel Lasker' .
	 ?game prop:result '1-0' .
	 ?game prop:result ?lose .
	}
	UNION
	{?game prop:white 'Wilhelm Steinitz' .
	 ?game prop:black 'Emanuel Lasker' .
	 ?game prop:result '0-1' .
	 ?game prop:result ?lose .
	}
	UNION
	{?game prop:white|prop:black 'Wilhelm Steinitz' .
	 ?game prop:white|prop:black 'Emanuel Lasker' .
	 ?game prop:result '1/2-1/2' .
	 ?game prop:result ?draw .
	}
}

========================================================
40	Two games with doubling rooks on the 7th rank from June 1894.

SELECT DISTINCT ?game
WHERE {
	?game prop:moves ?moves .
	?game prop:date ?date .
	?moves prop:fen ?fen .
	FILTER (regex(?fen , '^.*/.*R{1}.*R{1}.*/.*/.*/.*/.*/.*/.* ') && regex(?date, '06.18945$'))
}

========================================================
41	Examples of games with bishop against knight and 2 pawns.
SELECT DISTINCT ?game 
WHERE {
	{?game prop:moves ?moves .	
	?moves prop:fen ?fen .
	FILTER (regex(?fen, '^.*B{1}.* ') && regex(?fen, '^.*n{1}.* ') && regex(?fen, '^[^N]* ') && regex(?fen, '^[^b]* ') && regex(?fen, '^[^p]*p{2}[^p]* '))
	}
	UNION
	{?game prop:moves ?moves .	
	?moves prop:fen ?fen .
	FILTER (regex(?fen, '^.*b{1}.* ') && regex(?fen, '^.*N{1}.* ') && regex(?fen, '^[^n]* ') && regex(?fen, '^[^B]* ') && regex(?fen, '^[^P]*P{2}[^P]* '))
	}
}

========================================================
42	Show me 10 games with a long castling.
SELECT DISTINCT ?game
WHERE {
	?game prop:moves ?moves .
	?moves prop:move 'O-O-O' .
}
LIMIT 10 OFFSET 0

========================================================
43	In which game was the latest castling?
SELECT DISTINCT ?game
WHERE {
	?game prop:moves ?moves .
	?moves prop:move 'O-O-O' .
	?moves prop:moveNr ?nr .
}
ORDER BY DESC(?nr)
LIMIT 1 OFFSET 0

========================================================
44	All games of Wilhelm Steinitz between 1855 and 1870.
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT DISTINCT ?game
WHERE {
	?game prop:black|prop:white 'Wilhelm Steinitz' .
	?game prop:date ?date .
BIND( substr( ?date, 1, 4 ) as ?year )
	FILTER ((xsd:Integer(?year)>1855)&&(xsd:Integer(?year)<1870))
}
ORDER BY ASC(?date)


========================================================
45 Show me all games from Amsterdam.
SELECT ?game
WHERE {
	?game prop:site 'Amsterdam'.
}

========================================================
46	In which game did the latest exchange take place?
SELECT DISTINCT ?game
WHERE {
	?game prop:moves ?moves .
	?moves prop:move ?move .
	?moves prop:moveNr ?nr .
	FILTER regex(?move, 'x', 'i')
}
ORDER BY DESC(?nr)
LIMIT 1 OFFSET 0

========================================================
47	All games in which a FM has defeated a GM.

SELECT DISTINCT ?game
WHERE {
	{?game prop:whiteTitle 'FM' .
	?game prop:blackTitle 'GM' .
	?game prop:result '1-0' .
	}
	UNION
	{?game prop:whiteTitle 'GM' .
	?game prop:blackTitle 'FM' .
	?game prop:result '0-1' .
	}
}

========================================================
48	What is played against Giuoco Piano?
SELECT DISTINCT ?game
WHERE {
	?game prop:eco ?eco .
	FILTER regex(?eco, 'C5[034].*')
}

========================================================	
49	In which variation in the Sicilian Defense does black win the most?
SELECT DISTINCT ?eco (count(?eco) AS ?wins)
WHERE {
	?game prop:eco ?eco .
	?game prop:result '0-1' .
	FILTER regex(?eco, 'B[2-9][0-9].*')
}
GROUP BY ?eco
ORDER BY DESC(?wins)
LIMIT 3 OFFSET 0

========================================================
50	What is the main variation in the Ruy Lopez?
SELECT DISTINCT ?eco (count(?eco) AS ?played)
WHERE {
	?game prop:eco ?eco .
	FILTER regex(?eco, '^C[6-9][0-9].*')
}
GROUP BY ?eco
ORDER BY DESC(?played)
LIMIT 1 OFFSET 0



